{
test(p,f) = setrand(1); a = ffgen(p^f, 'a); [
a^2+3*a+1,
a/(1+a),
2*(a+1)/3,
1/5+a,
if (6*a, 5/6/(a+1)),
if (6*a, 5/6*(a+1)),
shiftmul(a+1,10),
if (2*a, shiftmul(a+1,-10)),
a^-1,
-a,
sqr(a),
sqrt(a^(2^10)),
sqrtn((a^2+a+1)^3,3),
sqrtn((a^2+a+1)^3,3,&z),
z,
if (ispower(a,3), a^(2/3)),
norm(a^2+1),
trace(a),
charpoly(a),
minpoly(a),
conjvec(a),
factor(x^6-a*x^3+1),
ellinit([a,1]),
a/x,
(x+a)/(x-a),
b=ffprimroot(a),
fforder(a),
b^fflog(a,b),
factorff(x^2+x+a),
polrootsff(x^2+x+a)
];
}
default(echo,1);

test(2, 20)
test(7, 7)
test(precprime(2^32), 3)
test(nextprime(2^32), 3)

test2(p)=
{
  ffgen(x*Mod(1,p));
  g = ffprimroot(ffgen((x+1)*Mod(1,p)), &o);
  print([g, o]);
  fflog(g^17, g, o);
}
test2(2)
test2(3)
test2(46744073709551653)
test2(precprime(1<<32))

for(i=1,10,print(ffnbirred(11,i)));
for(i=1,10,print(ffnbirred(11,i,1)));

do(f,p,T)=centerlift(lift(polrootsff(f,p,T)));
do(x^3+x^2+x-1,3,t^3+t^2+t-1)
t = ffgen(3^3,'t); do((x^3+x^2+x-1)*t^0, t.p, t.mod)
polrootsff(x^4+1,2,y^2+y+1)

t = ffgen(7^4); fflog(t^6,t^2)

t = ffgen(2^64)^((2^64-1)\5);1/t

t = ffgen(('t^2+'t+1)*Mod(1,2));
factorff(x^12 + t*x^10 + x^6 + (t+1)*x^2 + 1)

\\ #1241
polrootsff(x^2 - x - ffgen((v^2+1) * Mod(1,3)))
\\ #1350
polrootsff(2*x+1,2,y)
sqrt(Mod(-1,4296540161))
sqrt(Mod(-1,18446744073944432641))
centerlift(factorcantor(prod(i=-10,10,(x^2-i)),2^64+13)[,1])
#polrootsff(x^107+2*x^3+1,3,ffinit(3,107,'a))
t = ffprimroot(ffgen(2^61)); fflog(t^1234567891012345678,t)
t = ffprimroot(ffgen(3^23)); fflog(t^12345678910,t)
t = ffprimroot(ffgen(5^23)); fflog(t^1234567891012345,t)
t = ffprimroot(ffgen(5^17)); fflog(t^123456789101,t)
ffgen(x^2+x+Mod(1,3))
conjvec(Mod(x, x^2+Mod(1,3)))
t = ffgen(5^4,'t);
factor((x^24-1)*t^0)
factorff(Pol(0),t.p,t.mod)
factorff(Pol(1),t.p,t.mod)
factorff(x^4-t,t.p,t.mod)

test(q)=
{
  my(t = ffgen(q,'t), m=[t,t^2,1+t^3; 1+t,1+t^2,1+t^3]);
  print(matker(m));
  print(matimage(m));
  print(matrank(m));
  my(M = [t,2*t^0,3*t^0; t,t^2,1+t^3; 1+t,1+t^2,1+t^3]);
  print(matdet(M));
  print(M^(-1)*M);
  my(v = [t^0, t^1, t^2]~);
  print(M*v);
}
test(2^5)
test(7^5)
test((2^64+13)^5)

p=2^64+13; g=ffprimroot(ffgen(p^2), &o); a=2*g^0;
v=[I,-1,Mat(1),matid(2)/2];
for(i=1,#v, print(iferr(fflog(a,g,v[i]),E,E)));
g^fflog(a,g,o) == a
